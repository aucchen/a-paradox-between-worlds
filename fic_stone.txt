*temp s1_see false
*temp s1_hear false
*temp s1_smell false
*temp s1_touch false
*temp s1_taste false

*page_break $!{protag_e} see@{protag_plural |s}...

[b]Universe - Noncanon?[/b]

You are in what appears to be another place. It is a bright world, gleaming under the [b]light[/b] of the sun.

What is this place?

*choice
    #A city [A/N: Modern AU, urban].
        *set ch1_setting "city"
        *goto investigate_city
*comment    #A small town or village [A/N: Medieval/Fantasy AU].
*comment        *set ch1_setting "town"
*comment        *goto investigate_village
*comment    #A strip mall [A/N: Modern AU, suburban].
*comment        *set ch1_setting "mall"
*comment        *goto investigate_mall
*comment    #It doesn't matter.
*comment        *set ch1_setting "city"
*comment        *goto investigate_city

*comment TODO: mall

*label investigate_city

Well, obviously, it's a different place. For one, you're outside, under the blue sky. The place appears to be a city, but it's certainly different from any city you've ever had the chance to encounter. The buildings are all different, the vehicles all strange, the people's clothes bizarre. Your group must stand out in their uniforms, but no one is looking at you.

*choice
    #Investigate this world.
        *temp look false
        *temp listen false
        *temp smell false
        *temp taste false
        *temp touch false
        *temp think false
        *label fic_pt2_describe_city
        You are obviously in another place, nowhere on Byblos that you know of.

        You see the blue sky, devoid of clouds, with a single yellow sun that looks like any other yellow star at a life-sustaining distance. You see tall buildings of what seem to be metal and glass, taller than 10 stories tall, rising on both sides. You see motorized vehicles of all colors, moving carefully within painted lines and moving with the road control signals. You see humans, or entities that look exactly like humans, wearing clothes for which you have no reference.

        *if listen
            You hear the overwhelming din of traffic, of what must be chemical-combustion-powered vehicles clattering down the streets. There are also the softer sounds of conversation and footsteps.

        *if smell
            The acrid odor of combustion is the dominant smell in this place.

        *if taste
            Why would you taste anything? Would it be an aftereffect of traversing the portal that led you to this world? Maybe you taste... cherries? Let's go with that. [A/N: it has been canonically confirmed that cherries exist in the Nebulaverse. See Book 3 Part 1.] But why? Why would it taste like cherries?

        *if touch
            You touch the ground. It is cold and rough and dirty. Why did you touch the ground?

        *if think
            You think about your present situation. It's obvious that you've traversed through a portal embedded in the Kingdomite device, one that lead you to another world. But you have no idea where you are right now.
        *choice
            *disable_reuse #Listen.
                *set listen true
                *goto fic_pt2_describe_city
            *disable_reuse #Smell.
                *set smell true
                *goto fic_pt2_describe_city
            *disable_reuse #Taste.
                *set taste true
                *goto fic_pt2_describe_city
            *disable_reuse #Touch.
                *set touch true
                *goto fic_pt2_describe_city
            *disable_reuse #Think.
                *set think true
                *goto fic_pt2_describe_city
            *disable_reuse *if (think) #Despair.
                This sucks. This absolutely sucks. You're trapped in some unknown place with absolutely no way of getting back.
                *set ch1_wins + 1
                *goto fic_pt2_continue
            #Continue.
                *if ((taste and touch) and (listen))
                    *set ch1_wins + 1
                *goto fic_pt2_continue
    #[Continue.]
        *set ch1_fails + 1
        *goto fic_pt2_continue

*label investigate_village

*comment TODO: senses

*choice
    #Investigate this world.
        *temp listen false
        *temp smell false
        *temp taste false
        *temp touch false
        *temp think false
        *label fic_pt2_describe_village
        You are obviously in another place, nowhere on Byblos that you know of.

        You see the blue sky, devoid of clouds, with a single yellow sun that looks like any other yellow star at a life-sustaining distance. You see buildings of wood and brick and stone, covered with white plaster.

        *if listen
            It's quiet here.

        *if smell
            Someting in the air smells disgusting.

        *if taste
            Why would you taste anything? Would it be an aftereffect of traversing the portal that led you to this world? Maybe you taste... cherries? Let's go with that. [A/N: it has been canonically confirmed that cherries exist in the Nebulaverse. See Book 3 Part 1.] But why? Why would it taste like cherries?

        *if touch
            You touch the ground. It is cold and rough and dirty. Why did you touch the ground?

        *if think
            You think about your present situation. It's obvious that you've traversed through a portal embedded in the Kingdomite device, one that lead you to another world. But you have no idea where you are right now.
        *choice
            *disable_reuse #Listen.
                *set listen true
                *goto fic_pt2_describe_village
            *disable_reuse #Smell.
                *set smell true
                *goto fic_pt2_describe_village
            *disable_reuse #Taste.
                *set taste true
                *goto fic_pt2_describe_village
            *disable_reuse #Touch.
                *set touch true
                *goto fic_pt2_describe_village
            *disable_reuse #Think.
                *set think true
                *goto fic_pt2_describe_village
            *disable_reuse *if (think) #Despair.
                This sucks. This absolutely sucks. You're trapped in some unknown place with absolutely no way of getting back.
                *set ch1_wins + 1
                *goto fic_pt2_continue
            #Continue.
                *if ((taste and touch) and (listen))
                    *set ch1_wins + 1
                *goto fic_pt2_continue
    #[Continue.]
        *set ch1_fails + 1
        *goto fic_pt2_continue

*label investigate_mall

Well, obviously, it's a different place. For one, you're outside, under the blue sky.

*comment TODO: senses

*choice
    #Investigate this world.
        *temp look false
        *temp listen false
        *temp smell false
        *temp taste false
        *temp touch false
        *temp think false
        *label fic_pt2_describe_mall
        You are obviously in another place, nowhere on Byblos that you know of.

        *if look
            You see the blue sky, devoid of clouds, with a single yellow sun that looks like any other yellow star at a life-sustaining distance. You see tall buildings of what seem to be metal and glass, taller than 10 stories tall, rising on both sides. You see motorized vehicles of all colors, moving carefully within painted lines and moving with the road control signals. You see humans, or entities that look exactly like humans, wearing clothes for which you have no reference.

        *if listen
            You hear the overwhelming din of traffic, of what must be chemical-combustion-powered vehicles clattering down the streets. There are also the softer sounds of conversation and footsteps.

        *if smell
            The acrid odor of combustion is the dominant smell in this place.

        *if taste
            Why would you taste anything? Would it be an aftereffect of traversing the portal that led you to this world? Maybe you taste... cherries? Let's go with that. [A/N: it has been canonically confirmed that cherries exist in the Nebulaverse. See Book 3 Part 1.] But why? Why would it taste like cherries?

        *if touch
            You touch the ground. It is cold and rough and dirty. Why did you touch the ground?

        *if think
            You think about your present situation. It's obvious that you've traversed through a portal embedded in the Kingdomite device, one that lead you to another world. But you have no idea where you are right now.
        *choice
            *disable_reuse #Look.
                *set look true
                *goto fic_pt2_describe_mall
            *disable_reuse #Listen.
                *set listen true
                *goto fic_pt2_describe_mall
            *disable_reuse #Smell.
                *set smell true
                *goto fic_pt2_describe_mall
            *disable_reuse #Taste.
                *set taste true
                *goto fic_pt2_describe_mall
            *disable_reuse #Touch.
                *set touch true
                *goto fic_pt2_describe_mall
            *disable_reuse #Think.
                *set think true
                *goto fic_pt2_describe_mall
            *disable_reuse *if (think) #Despair.
                This sucks. This absolutely sucks. You're trapped in some unknown place with absolutely no way of getting back.
                *set ch1_wins + 1
                *goto fic_pt2_continue
            #Continue.
                *if ((taste and touch) and (listen))
                    *set ch1_wins + 1
                *goto fic_pt2_continue
    #[Continue.]
        *set ch1_fails + 1
        *goto fic_pt2_continue



*label fic_pt2_continue

"There's no sign of the portal, is there?" asks Tycho. "Does anyone know how we'll get back?"

Capella is already moving. She points at a person, and you all freeze.

*fake_choice
    *if (protag_likes_astra=1) #It's... Astra?
        Somehow, it appears to be another Astra. You look to Astra, and then her. No doubt; they look identical save their clothes, down to the hairstyle. The other Astra isn't looking in your direction. She soon disappears inside a building.
    *if (protag_likes_bruno=1) #It's... Bruno?
        Somehow, it appears to be another Bruno. You look to Bruno, and then him. No doubt; they look identical save their clothes, down to the hairstyle. The other Bruno isn't looking in your direction. He soon disappears inside a building.
    *if (protag_likes_Tycho=1) #It's... Tycho?
        Somehow, it appears to be another Tycho. You look to Tycho, and then him. No doubt; they look identical save their clothes, down to the hairstyle. The other Tycho isn't looking in your direction. He soon disappears inside a building.
    *if (protag_likes_capella=1) #It's... Capella?
        Somehow, it appears to be another Capella. You look to Capella, and then her. No doubt; they look identical save their clothes, down to the hairstyle. The other Capella isn't looking in your direction. She soon disappears inside a building.
    #It's... you?
        Somehow, it appears to be another you. Yes, the person in question is your mirror image, down to the hairstyle, save for the clothes ${protag_e} wear@{protag_plural |s}. But soon ${protag_e} disappear@{protag_plural |s} inside a building.
    
    
Capella is already in pursuit. She follows the doppelganger into the building, and you join the rest of the group.

Inside, however, the doppelganger is nowhere to be seen. It's a busy place, an alien food dispensary of sorts, filled with tables and lined with customers. None of them take any notice of you. They act as if you don't even exist.

"Hello? Anyone home?" shouts Capella, but no one even turns their head.

*comment TODO: loop of different options/senses

*choice
    #Look around.
        You see what appears to be the staff of the refectory. They look like... well, you could have guessed it. They look like the five of you.
    #Try shouting.
        "Hello?" you shout, but of course no one hears you. Someone approaches you as if you don't exist. Your body passes right through theirs, without a sensation.
    #Try touching.
        The tables and countertops feel normal. So you walk up to a random person and try to touch them. Your hand passes right through, as if either they didn't exist or you didn't exist.
    #Try using magic.
        You recall a basic illumination formula. It works; the light shines in the center of the room, but no one notices, even though it should be almost blinding.
        
        
        Capella casts a wind spell, and while you feel the breeze, again, it has no impact on the world.
    
    
Astra is closing her eyes and taking deep breaths. "Okay, I'm trying to think through this. When we came through the Kingdomite portal, we ended up at... a different universe. I've read about this."

"Real helpful," Bruno remarks.

"Quiet. I'm thinking here. Um... parallel universes. They exist. It's like we thought."

"They sure do," Capella says.

"So, is there any way we can go back?" Tycho asks.

Capella has her arms folded, pacing, not caring that she's phasing through the passersby. "Why are we still here? We should be exploring this whole world."
    
    
*choice
    #[META: Deus Ex Machina]
        *goto deus_ex_machina
    *selectable_if (has_chekhovs_gun) #[META: Chekhov's Gun]
        *set ch1_wins + 1
        *goto chekhovs_gun

*label chekhovs_gun

You remember the pamphlet Astra gave you about the Kingdom's multiverse experiments, and the spells she tried to teach you.

"Hey Astra?" you ask. "Can you try using the spells you mentioned?"

"I [i]am[/i] trying them!" she says in an exasperated voice. "I've been trying them the whole time we've been here and it hasn't been working!"

*page_break Try the spells yourself.

You close your eyes. You shut out every noise. You recite the phrases in your mind.

It's hard to tell when a spell is working, especially one as abstract as this one. But at one point, you start to feel something. It's a strange and unintelligible feeling, As if everything in this world just suddenly became more real. As if you could break through whatever barrier separates you from the people here. If only...

You open your eyes. "Gali? What are you doing?" Astra calls. But you're already moving forward, walking up to the counter where your doppleganger stands.

*goto talk_to_dp

*label deus_ex_machina
*set ch1_fails + 1
A feeling emerges from within you. It's a strange and unintelligible feeling, As if everything in this world just suddenly became more real. As if you could break through whatever barrier separates you from the people here. If only...

*label know_what_to_do
*choice
    #Talk to your doppelgangers.
        "Gali? What are you doing?" asks Astra as you walk up to the counter. You don't reply. You walk up, until you see your mirror image standing behind the counter in some sort of uniform. You focus. You feel a shimmering all around you. And then...
        *goto talk_to_dp
    #Tell your friends.
        "I know what to do," you say.
        
        
        "What do you mean?" asks Astra.
        
        
        "It's... I have a feeling. There's a barrier here, and I can break through."
        
        
        "How? How do you know?"
        
        
        "I don't. It's just... a feeling, I guess?"
        
        
        Capella and Bruno are looking at you skeptically. But you know what to do. You walk up, until you see your mirror image standing behind the food dispensary counter wearing some kind of uniform. You focus. You feel a shimmering all around you. And then...
        *goto talk_to_dp
    #Focus. Just focus.
        "Gali? What are you doing?" asks Astra as you walk up to the counter. You don't reply. You walk up, until you see your mirror image standing behind the counter in some sort of uniform. You focus. You feel a shimmering all around you. And then...
        *goto talk_to_dp
        
        
*label talk_to_dp
*page_break You break through.

"Hi! What can I get you?" ${protag_e} ask@{protag_plural |s}. Then ${protag_eir} eyes open wide.

"You... you shouldn't be here," ${protag_e} say@{protag_plural |s}. "This isn't right. This isn't your story."

*fake_choice
    #"Wait, what do you mean?"
        "What do you mean?" you say.
    #"How do we get back?"
        "How do you get back?" you manage to shout.
    #"Where are we?"
        "Where are we?" you manage to shout.

But ${protag_e} @{protag_plural don't|doesn't} answer your question.

"Things were good before you came.
*if (fic_ship = "brunova")
    Bruno and I just started dating. We're building a life here.
*elseif (fic_ship = "tychonova")
    Tycho and I just started dating. We're building a life here.
*elseif (fic_ship = "astranova")
    Astra and I just started dating. We're building a life here.
*elseif (fic_ship = "ellanova")
    Ella and I just started dating. We're building a life here.
*elseif (fic_ship = "astrapella")
    Astra and Capella just got together. They're building a life together.
*elseif (fic_ship = "brunastra")
    Bruno and Astra got together. They're building a life together.
*elseif (fic_ship = "brunotycho")
    Bruno and Tycho got together. They're building a life together.
*else
    We were doing fine all by ourselves.
We didn't need you. Not every world is yours. Not every story is yours to own." $!{protag_e} @{protag_plural stare|stares} at you, ${protag_eir} gaze stern.

"Go now. Get out."

You feel a sudden anxiety rising through your body, and a pulsating headache. You stagger backwards, not entirely of your own volition.

"Gali? What's wrong?" Astra asks, but you don't have time to answer.

It's as if an invisible force is dragging you out, onto the street, phasing through the walls. The ground trembles, and opens up with a horrible creaking noise. You fall into the fissure. You see your friends rush towards you, until the earth opens up to swallow them too.

*page_break Falling. Endless falling.

[b]IRL?[/b]
*comment random roll for popularity
*temp roll 1
*rand roll 1 6
*set popularity + ch1_wins
*set popularity - ch1_fails
*if (popularity < 0)
    *set popularity 0
*set ch1_likes ((popularity+1)*(2*roll))
*set ch1_rbs ((popularity+1)*(roll-1))

*temp new_followers ((popularity + roll) - 6)
*if (new_followers<0)
    *set new_followers 0
*set followers + new_followers

*temp luna_message_seen true
*temp claire_message_seen true

Well, that was an unsatisfying ending for a first chapter.

*fake_choice
    #Actually, it's fine.
        Is it?
    #Done is better than perfect.
        It's always better to have a story out there than a story that only exists in your head. Is that right?
    #But you'll get better, right?
        This is just a first step, right? It's setting a low floor for yourself, right? You can only get better, right?
    #Did you even get to the ships?
        Well, not enough. But hopefully this is an interesting and unique enough concept that it survives. Leaning on ships is an awfully cheap strategy for grabbing attention.

Anyway, you hit the publish button. It's live. Now, you just have to wait for the reaction...

*page_break Another day, another time...

[b]Online: Community[/b]

The story has been posted for a while now. You see that you've gotten ${ch1_likes} likes and ${ch1_rbs} reblogs on your story. @{new_followers>0 You also got ${new_followers} new followers!|}
*if (ch1_likes >= 100)
    That's a very high number! Your story is somehow very popular!
*elseif (ch1_likes >= 50)
    That's a pretty decent number! Your story is somehow pretty popular!
*elseif (ch1_likes >= 25)
    That's not a bad number of likes!
*elseif (ch1_likes >= 10)
    Well, that's a decent number.
*else
    Less than 10 likes is... not a good sign. But you'll get over it.

*temp chat_count 0
*if (claire_blue > claire_green) and (claire_blue > 15)
    *set claire_chat true
    *set claire_message_seen false
    *set chat_count + 1
*if (luna_blue > luna_green) and (luna_blue > 25)
    *set luna_chat true
    *set luna_message_seen false
    *set chat_count + 1
*comment TODO: add lux, stella, maybe karla
*if (karla_blue > karla_green) and (protag_trans_girl and (new_followers > 0))
    *set karla_blue %+ 8
    *set karla_mutuals true

*if chat_count = 2
    Also, you have some new private messages! One is from Luna @lunan0va and the other is from Claire @shadow-protectrix.
*elseif chat_count = 1
    Also, you have a new private message! It's from 
    *if claire_chat
        Claire @shadow-protectrix
    *if luna_chat
        Luna @lunan0va
    !
*elseif chat_count = 0
    Unfortunately, you haven't gotten any new messages regarding your fanfic. It's only to be expected, given how hard it is to get any attention these days.
    
    
*label messages
*choice
    *if (not (luna_message_seen)) #See Luna's message.
        *goto luna_message
    *if (not (claire_message_seen)) #See Claire's message.
        *goto claire_message
    *if (luna_message_seen and claire_message_seen) #Done with messages.
        *goto mv1_end

*label luna_message
*temp luna_friends false
*set luna_message_seen true
@lunan0va: hey ${handle}!

@lunan0va: i've read your story and it's really cool! i love the concept!

*if protag_trans_girl
    @lunan0va: also sorry if this is weird but it's kinda cool that you made gali a trans girl here. just another bonus :)

*if ((luna_blue - luna_green) >= 20)
    *set luna_friends true
    @lunan0va: anyway i'm glad we can be friends.
    
    
    *if protag_trans_girl
        @lunan0va: you make me feel a little less lonely 💙️
        
*choice
    #"Thanks!"
        @you: thanks!
        
        
        @lunan0va: np!!! i should thank you for writing it!
    *if (protag_trans_girl) #"I love your headcanon!"
        @you: i really love your headcanon!
        
        
        @lunan0va: thank you!!!
    *if (luna_friends) #"I'm glad we can be friends, too..."
        *set luna_blue %+ 3
        @you: i'm glad we can be friends, too...
        
        
        @lunan0va: 💙️
        
        
        @lunan0va: yea...

*if luna_friends
    *set luna_chat_invited true
    @lunan0va: btw, if you're interested!!!

    @lunan0va: i'm in a group chat for just talking about the nebulaverse and stuff.

    @lunan0va: i can send you a link to join if you want?
    
    
    *choice
        #Sure, join!
            *set in_luna_chat true
            @you: Sure...

            @lunan0va: great!!! here's the link: [link]

            @lunan0va: anyway it's great! feel free to drop in!
        #Nah.
            *set luna_green %+ 3
            @you: Um, not really...

            @lunan0va: alright that's fine!

            @lunan0va: but feel free to message me later if you want...

Luna doesn't seem to have much more to say for now, but maybe you do?

*choice
    #Just talk about stuff?
        *label luna_random_talk
        *set luna_blue %+ 8
        
        @you: so um... hows life?
        
        @lunan0va: its okay... mostly the same as usual.
        
        @lunan0va: school is destroying me :/
        
        @lunan0va: what about you? how are things irl?

        *comment TODO
        *fake_choice
            #I don't really want to talk about IRL stuff.
                @you: um... i don't really want to talk about irl stuff.

                @lunan0va: that's okay!

                @lunan0va: do you want to talk about nebulaverse stuff instead?
                *choice
                    #Sure! (ask about Luna's fic)
                        @you: sure! how are things with your fic?

                        @lunan0va: i've been working on my fic again...
                        *goto luna_fic_talk
                    #Nah, I don't really have anything to talk about...
                        @you: nah, i don't really have anything to talk about

                        @lunan0va: lol i get that.
                        *goto luna_message_end
            #There's not much going on.
                @you: um... there's not really much going on.

                @you: life is... pretty much the same as usual.

                @lunan0va: haha yeah... it really feels that way.

                @lunan0va: between parents and school it's like... idk, everything is exactly the same all the time.

                @lunan0va: it's like reading fics and looking at art and posts on here are the only things keeping me sane right now.

                @you: yeah...
            #Let's talk about nebulaverse stuff instead!
                @you: let's talk about nebulaverse stuff instead!

                @you: how's your new fic going?
                *label luna_fic_talk

                @lunan0va: i have no idea what to do with it lol.

                @lunan0va: i'm pretty sure it just sucks :/

                *if luna_ch1_fic_good
                    @lunan0va: i know you said it was good but like... idk
                *else
                    @lunan0va: i'm trying to change and improve it like you said but idk how. it's just like... a total mess.
        
        *goto luna_message_end
    #Confess your love.
        Wait... What? You are not ready for that conversation, not even close.
        *choice
            #Send a small message that could have the potential of starting a romance later on.
                Okay, uh... you can send a heart emoji? That's how it works, right? You send people enough heart emojis and then they fall in love with you?
                
                You have to pick a signature heart emoji.
                *gosub_scene hearts_choice
                *achieve love
                        
                @you: ${heart_emoji}${heart_emoji}${heart_emoji}
                
                @lunan0va: thanks! 💙️${heart_emoji} to you too...
                
                Unfortunately, you do not know what to say after that.
                *set luna_red %+ 1
                *set luna_blue %+ 2
                *goto luna_message_end
            #Forget about it.
                *goto luna_message_end
            #Just talk about random stuff?
                *goto luna_random_talk
    #Let the conversation end.
        Sometimes, when you don't have anything to say, you don't have anything to say. 
        *goto luna_message_end

*label luna_message_end
*set chat_count - 1
*goto messages

*label claire_message
*set claire_message_seen true
*set claire_mutuals true

You also see that Claire has followed you. They're probably by far your most popular follower right now. So that's pretty cool.

@shadow-protectrix: I just saw your new fanfic, ${story_title}, and it looks pretty interesting so far! I'd love to see more from you!

@shadow-protectrix: By the way, would you be interested in joining my groupchat for Nebulaverse fandom writers? We're a pretty small group of writers who're interested in improving our craft and just hanging out and talking about stories.

@shadow-protectrix: No pressure! Just something I felt like you could be interested in!

*fake_choice
    #Of course I'd like to join!
        *set in_claire_chat true
        *set claire_blue %+ 5
        @you: Of course I'd like to join!
        
        
        @shadow-protectrix: Great! [Here's the link!]
    #Nah, I'd rather not.
        *set claire_green %+ 5
        @you: Sorry, I'd rather not...
        
        
        @shadow-protectrix: Well, that's okay. If you ever want to, just message me here!
    #Can I decide later?
        @you: Sorry, can I decide later?
        
        
        @shadow-protectrix: Sure! Just let me know here if you ever want to join.
        
        
Do you want to talk more to Claire about anything?

*choice
    #Talk about Nebulaverse stuff? Compliment her fics?
        *set claire_blue %+ 3
        @you: I really enjoy your stories...
        
        
        @shadow-protectrix: Thank you!!!

        *label claire_nebulaverse
        *choice
            #Ask about some Nebulaverse development?
                *set claire_blue %+ 3
                @you: um, can I ask you something about the nebulaverse?

                @shadow-protectrix: Sure, go ahead!

                You wrack your brain for a topic...
                *fake_choice
                    #Do you think Brunova will be canon?
                        @you: um, do you think Brunova will be canon?

                        @shadow-protectrix: haha

                        @shadow-protectrix: I really doubt it! But isn't the whole point of writing fanfiction to get around what's canon?

                        @shadow-protectrix: It would be really boring if we had to rely on what the author says rather than creating our own interpretations!

                        @you: yeah...

                    #What do you think about Astranova or Ellanova?
                        @you: um, what do you think about astranova or ellanova?

                        @shadow-protectrix: haha

                        @shadow-protectrix: It's pretty obvious Ellanova is gonna be canon.

                        @shadow-protectrix: But that doesn't matter for fanworks, does it? I enjoy both a lot!
                    #What do you think about Astrapella?
                        @you: um, what do you think about astrapella?

                        @shadow-protectrix: haha

                        @shadow-protectrix: It's great! It's my favorite femslash ship!

                        @shadow-protectrix: I haven't written for it because I don't have any ideas, but I do enjoy it!
                    #What do you think will happen in Book 5 Part 2?
                        @you: um, what do you think will happen in Book 5 Part 2?

                        @shadow-protectrix: LOL we're all thinking about that.

                        @shadow-protectrix: Well, of course I don't have any real idea. But if I had to guess, I'd guess that there will definitely be more Chronicles coming up afterwards. There's no way that all the plot threads will be wrapped up.

                        @shadow-protectrix: Maybe that's unsatisfying but that's what I think will happen.
                    #What do you think happened in Book 4 Part 4?
                        @you: um, what do you think happened in Book 4 Part 4?

                        @shadow-protectrix: LOL we're all thinking about that.

                        @shadow-protectrix: Well, obviously all of the events depicted in that book were in their heads.

                        @shadow-protectrix: The book basically gave us the raw feeds to their brains, all the images and thoughts, all represented literally.

                        @shadow-protectrix: It's kind of weird but I'm into it!

                        @you: yeah...
                 

                Claire seems to have gone offline.
            #Let the conversation end.
                Sometimes, when you don't have anything to say, you don't have anything to say.
        *goto claire_message_end
    #Talk about life?
        *set claire_blue %+ 3
        You don't really know what to say to them because, well, they're very much an adult and you're not.

        @you: So, um, how do you think of what to write?

        @shadow-protectrix: It's hard sometimes! I guess I just read a lot? And get inspirations from reading other books and stories!

        @shadow-protectrix: Not just Nebulaverse stuff, actual novels too...
        *goto claire_nebulaverse
    #No, let the conversation end.
        Sometimes, when you don't have anything to say, you don't have anything to say.
        *goto claire_message_end

*label claire_message_end
*set chat_count - 1
*goto messages

*label mv1_end
*return
