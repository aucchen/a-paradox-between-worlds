*script setMbmMode(false);

[b]Coda: 6 months later[/b]

[i]"I think we deserve a soft epilogue, my love. We are good people and we've suffered enough."[/i]

- Seventy Years of Sleep, nikka ursula


[i]ROSE: I have no comprehensible path. There's nothing to overcome, no lesson to learn, no cathartic light at the end of this preposterous tunnel.[/i]

[i]ROSE: Not for me, at least![/i]

[i]ROSE: I seriously have the DUMBEST arc anyone could conceivably imagine.[/i]

[i]DAVE: rose we dont have fuckin "arcs" we are just human beings[/i]

- Homestuck by Andrew Hussie

*set luna_bio "If I don't know you, you shouldn't even be reading this."

*set karla_bio "No longer trapped in space, but I still use this url for some reason. Karla, she, 22. f*ck m*cmillan as always."

*set lux_bio "no [i]light[/i], no [i]light[/i] (Lux/Lucas, 18, he/him, bi mlm - yes i know i have this url but i am not interested in talking about the n*bulaverse ever again. fuck macm, all day every day.)"

*set claire_bio "I'm Claire, your favorite nonbinary aspiring novelist. They/them, perpetually failing at adulting."

*page_break

*comment TODO
[b]Universe - Noncanon?[/b]

[b]Excerpt from "Holding Hands at the End of the World" by @lunan0va (orphan_account)[/b]

*comment this is based on one of my previous fics - the aravris one.

The stars are disappearing one by one. Astra and Capella are watching, laying on their backs on Academy Hill. They are close enough that their bodies are touching, their hands intertwined.

"Did you know that this would all happen?" asks Astra, turning to face the other girl, who is still staring at the sky. "How many times have you gone back? Has it always been the same outcome?"

"Every world's been different, but it all leads here," says Capella. "The beginnings and middles are different, but the endings are all the same. Every time I've gone back, it ends like this."

"Oh." Astra is silent for a moment. "So there's no hope of escaping the cycle, then?"

"I don't know yet." Capella sighs. "Maybe."

*page_break The planet is on fire.

The planet is on fire. In the periphery of her vision, Capella sees an orange glow as the Academy buildings begin to immolate. Everyone else she knows is likely dead. The school uniform's long socks are uncomfortably warm.

"Hey, can I ask you something else? How do you keep going? When you've seen the end of the world so many times, how do you still manage to do anything at all?"

Capella turns to face Astra. "It's nice to be around someone I care about. That's all I need. Every moment is like..."

The distance between them is vanishing. Astra's eyes are watering. Capella wipes away the tears with her free hand.

"Oh, Ella, I-"

And then-

[i]fin[/i]

***

A/N: thanks for reading! please leave a comment if you enjoyed this! (um, for various definitions of "enjoyed" i guess???? sorry about the angst.... i've been, uh, feeling a certain way lately and this is how i deal with it i guess hahahaha....)

- luna

*page_break
*script setMbmMode(true);

*temp read_count 0
*temp luna_prog 1
*temp brun_prog 1
*temp shad_prog 1
*temp nebu_prog 1
*temp apoc_prog 1
*temp trap_prog 1

*temp luna_1_1_notes 0
*temp luna_1_2_notes 17231

*temp brun_1_1_notes 1
*temp brun_1_2_notes 1

*temp shad_1_1_notes 1
*temp shad_1_2_notes 1

*temp nebu_1_1_notes 1
*temp nebu_1_2_notes 1

*temp trap_1_1_notes 1
*temp trap_1_2_notes 1

*temp luna_message_ref "luna_message_1"
*temp lux_message_ref "lux_message_1"
*temp claire_message_ref "claire_message_1"
*temp sofia_message_ref "sofia_message_1"
*temp karla_message_ref "karla_message_1"

*temp annc_ref "annc_1"

*temp has_luna_message luna_end
*temp has_lux_message lux_end
*temp has_claire_message claire_end
*temp has_sofia_message sofia_end
*temp has_karla_message karla_end
*set has_karla_message false
*set has_sofia_message false
*set has_claire_message false
*set has_lux_message false

*temp luna_message_read false
*temp lux_message_read false
*temp claire_message_read false
*temp sofia_message_read false
*temp karla_message_read false


[b]Online - Montage[/b]

Online weather report: calm skies and empty blogs. The Nebulaverse fandom as it had existed is no more. Dead blogs lay like skeletal carcasses, and broken links litter the landscape like highway wreckages. Many people have simply moved on, either to other fandoms beyond the Nebulaverse, or away from this website altogether. The tide of art and fic have continued, but with a more somber, less triumphant tenor. There are still blogs posting new and ever more outrageous discourses, but you do not follow them.

*label montage_1

*if (has_luna_message and not(luna_message_read))
    There's a message from Luna @icemoongirl.

*if (has_lux_message and not(lux_message_read))
    There's a message from Lux @brunova-official.

*if (has_claire_message and not(claire_message_read))
    There's a message from Claire @shadow-protectrix.

*if (has_sofia_message and not(sofia_message_read))
    There's a message from a user named @pistis--sofia. Could it be Sofia @nebulascribbler, back from the dead?

*if (has_karla_message and not(karla_message_read))
    There's a message from Karla @trappedinspace.

Which blogs would you like to read?

*choice
    *selectable_if (luna_end) #@icemoongirl
        *set read_count + 1
        *gotoref "luna_1_${luna_prog}"
    *selectable_if (lux_end) #@brunova-official
        *set read_count + 1
        *gotoref "brun_1_${brun_prog}"
    *selectable_if (claire_end) #@shadow-protectrix
        *set read_count + 1
        *gotoref "shad_1_${shad_prog}"
    *if (sofia_message_read) #@pistis--sofia
        *set read_count + 1
        *gotoref "nebu_1_${nebu_prog}"
    *selectable_if (karla_end) #@trappedinspace
        *set read_count + 1
        *gotoref "trap_1_${trap_prog}"
    *if (in_luna_chat and (luna_end or (karla_end or lux_end))) #See Luna's groupchat (Anti-Nebulaverse Nebulaverse Club).
        *gotoref annc_ref
    *if (has_claire_message) #See Claire's message.
        *gotoref claire_message_ref
    *if (has_luna_message) #See Luna's message.
        *gotoref luna_message_ref
    *if (has_karla_message) #See Karla's message.
        *gotoref karla_message_ref
    *if (has_sofia_message) #See Sofia's message.
        *gotoref sofia_message_ref
    *if (read_count > 0) #Stop looking at blogs.
        *goto montage1_end

*label luna_1_1
*set luna_prog 1
[b]${luna_bio}[/b]

[b]@icemoongirl posted at 8:51 PM[/b]

Idk why but I feel like my parents are more... careful? around me? Like they're avoiding me or something. Especially my dad. It's honestly kind of creepy. We used to have conversations! Sure some of them degenerated into yelling sessions but at least I could tell how they felt! Of course maybe dad had an actual change of heart but nothing in my experience makes me believe he can change.

Is this because of what my siblings said to them? Did they threaten my parents? Is my dad living in fear of CPS right now? Lmao, as if CPS in Texas would ever help a trans kid instead of taking them from accepting parents.

Anyway, I can't wait to turn 18 and get out of this place.

Speaking of which, the SAT is done and my scores kind of suck! I'm applying to colleges now! The place I'm looking at is at least 100 miles away and has in-state tuition and isn't *that* selective lol

Tags: #stardust, #btw even if my parents weren't like this the state govt is literally banning me from transitioning, #one of the tr*nsphobic senators literally quoted g/t/m lmao, #its funny but its also super fucked up, #like everything's always getting worse, #(also i didn't want to put this tag on the last reblog but... that sort of shit makes me want to die.), #its just so much shit all the time!!! when will it ever stop!!!

${luna_1_1_notes} note@{luna_1_1_notes=1 |s}

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set luna_interact + 1
        *set luna_1_1_notes + 1
        *set luna_blue %+ 5
        *goto luna_1_1
    *disable_reuse #Reblog üîÅ
        *set luna_interact + 1
        *set luna_green %+ 5
        *set luna_1_1_notes + 1
        *goto luna_1_1
    #Next post.
        *goto luna_1_2
    #Look at another blog.
        *goto montage_1


*label luna_1_2
*set luna_prog 2

[b]@icemoongirl reblogged from @trappedinspace at 8:11 PM.[/b]

Content warnings: transphobia, transmisogyny, misgendering, discourse

*choice
    #Read the post.
        *goto luna_1_2_post
    #Look at something else.
        *goto montage_1

*label luna_1_2_post
[b]@icemoongirl reblogged from @violetdelights at 8:11 PM.[/b]

[b]@ayanamis02:[/b]

Why am I still seeing gifs of the sh*dow n*bula movie on my dash? Did people forget that g*tm is a transphobe?

[b]@lesbiantaikongs:[/b]

Reminder that gtm is a transphobe who supports transphobic politicians and misgendered a trans girl on her blog! Block me if you support gtm in any way.

[b]@bookishgirl93:[/b]

Why does no one on this website have critical thinking or reading comprehension skills? She never "misgendered" anyone, and she has always been a feminist who supports women's rights. She called out a trans male who was sending her rape and death threats. Are women not allowed to fight back against the men who abuse them on the internet?

[b]@amaranthine-shores:[/b]

Of course no one on this website has half a brain cell. They all believe their precious "trans women" (AKA trans-identified males) can do no wrong, so they short-circuit when they get a reality check.

[b]@lesbiantaikongs:[/b]

Okay, first of all fuck you, fuck terfs and all that. Second of all, where is the proof? Where's the goddamned proof of any of the things you're saying? Does no one remember how she called a trans girl a degenerate freak for thinking Capella could be trans? And how that trans girl got a shitton of harassment because gtm tagged her fucking blog and went on to call her a "violent male" and all that? That's the misgendering, by the way.

[b]@feministicemage:[/b]

Are you delusional? There is literally no evidence that Macmillan harassed or otherwise insulted anyone, and there is no evidence that she is a transphobe or "terf" of any kind. It's really quite Orwellian, the way you people are making up things to smear her. So-called activists on this website are gaslighting anyone who doesn't cave to their insane demands.

[b]@violetdelights:[/b]

Does no one here know how to read critically? There is thorough evidence of GTM's transphobic views, and how her posts lead to harassment. The details are off but the broad gist of what @lesbiantaikongs said is correct. And GTM's political alliances are all public knowledge.

By the way, that trans girl's name was Luna I think. Last I heard, she attempted suicide and deleted. I really hope she's doing okay.

[b]@stoneraugury:[/b]

Wasn't this person an abuser who had a bunch of sock puppet URLs that they used to send death threats to people? And they only said they attempted suicide to fish for sympathy when they got caught?

[b]@violetdelights:[/b]

I think you're confusing this with an entirely different situation.

Tags: #transphobia cw, #transmisogyny cw, #misgendering cw, #discourse cw, #...

${luna_1_2_notes} notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set luna_interact + 1
        *set luna_1_2_notes + 1
        *set luna_blue %+ 1
        *goto luna_1_2
    *disable_reuse #Reblog üîÅ
        *set luna_interact + 1
        *set luna_green %+ 1
        *set luna_1_2_notes + 1
        *goto luna_1_2
    #Prev post.
        *goto luna_1_1
    #Look at another blog.
        *goto montage_1


*label brun_1_1
*set brun_prog 1
[b]${lux_bio}[/b]

[b]@brunova-official posted at 7:00 PM[/b]

guys, i really fucking need a new url. help me out here. tell me which one you wanna see.

- @luzverde
*line_break
- @electronicghosts
*line_break
- @400luxofficial
*line_break
- @interstatefloodlights
*line_break
Any others?

Tags: #akl;dfjl;dskafjlsakjff, #these totally suck but i cannot keep on being brunova-official

${brun_1_1_notes} notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set brun_1_1_notes + 1
        *set lux_blue %+ 2
        *goto brun_1_1
    *disable_reuse #Reblog üîÅ
        *set lux_interact + 1
        *set lux_blue %+ 5
        *set brun_1_1_notes + 1
        *goto brun_1_1
    *disable_reuse #Comment: suggest a URL.
        Which URL do you suggest?
        *fake_choice
            #@luzverde
            #@electronicghosts
            #@400luxofficial
            #@interstatefloodlights
        Suggestion noted.

        *goto brun_1_1
    #Next post.
        *goto brun_1_2
    #Look at another blog.
        *goto montage_1

*label brun_1_2
*set brun_prog 2
[b]@brunova-official posted[/b]

FAQ for Untitled Videogame Comic:

- Q: What's your update schedule?

A: I'm trying to get a new page out every Saturday. I can't guarantee it due to work, school, and personal emergencies, but I'm gonna try.

- Q: Are any of the characters gay/trans/queer?

A: Yes.

- Q: Is [specific character] gay/trans?

A: Not telling.

- Q: Are Joey/Luis based on Brunova?

A: No. None of the characters in the comic are based on any existing properties.

- Q: What are the characters' canonical ethnicities?

A: Don't ask me about this.

- Q: What games is the Untitled Videogame based off?

A: Don't ask me about this.

Tags: #untitled videogame comic, #not actually a faq since i don't get frequent questions lmao

${brun_1_2_notes} notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set brun_1_2_notes + 1
        *set lux_blue %+ 4
        *goto brun_1_2
    *disable_reuse #Reblog üîÅ
        *set lux_interact + 1
        *set lux_blue %+ 5
        *set brun_1_2_notes + 1
        *goto brun_1_2
    #Prev post.
        *goto brun_1_1
    #Look at another blog.
        *goto montage_1


*label shad_1_1
*set shad_prog 1
[b]${claire_bio}[/b]

[b]@shadow-protectrix posted one day ago.[/b]

Anonymous asked: just in case you didn't know, there are some posts about you going around saying that you were transphobic and supported g/t/m when that happened. is anything about those posts true?

~sigh~ *deep breath* Oh boy. Oh man. I have been getting sporadic asks about this, and I've been lurking in the background of the discourse.

tl;dr: Yes, I was deeply involved in the nebulaverse fandom. I did some things that I now regret. I do not support gtm's views in any way.

(Read more)

${shad_1_1_notes} notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set claire_blue %+ 5
        *set shad_1_1_notes + 1
        *goto shad_1_1
    *disable_reuse #Reblog üîÅ
        *set claire_interact + 1
        *set shad_1_1_notes + 1
        *set claire_blue %+ 5
        *set karla_green %+ 3
        *set luna_green %+ 3
        *goto shad_1_1
    #Next post
        *goto shad_1_2
    #Look at another blog.
        *goto montage_1

*label shad_1_2
*set shad_prog 2
*set claire_ch5_seen 1

[b]@shadow-protectrix posted one day ago.[/b]

OMGOMGOMG I have to tell you guys... I have an agent!!!

Now, you kids might not know what this means. It means that *someone* has decided to take a chance on my novel. She's going to shop it around to different publishers in order to find one that thinks my novel will sell.

Now, what is this novel about? Well, you'll just have to wait! But here's a hint: Wind and Ice.

Tags: #claire speaks

${shad_1_2_notes} notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set shad_1_2_notes + 1
        *set claire_blue %+ 2
        *goto shad_1_2
    *disable_reuse #Reblog üîÅ
        *set claire_interact + 1
        *set shad_1_2_notes + 1
        *set claire_blue %+ 2
        *goto shad_1_2
    #Prev post.
        *goto shad_1_1
    #Look at another blog.
        *goto montage_1


*label nebu_1_1
*set nebu_prog 1
[b]${sofia_bio}[/b]

[b]@pistis--sofia posted at 5:00 PM[/b]


${nebu_1_1_notes} notes

Tags: #sofia speaks

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set nebu_1_1_notes + 1
        *goto nebu_1_1
    *disable_reuse #Reblog üîÅ
        *set sofia_interact + 1
        *set nebu_1_1_notes + 1
        *goto nebu_1_1
    #Next post.
        *goto nebu_1_2
    #Look at another blog.
        *goto montage_1


*label nebu_1_2
*set nebu_prog 2
*set sofia_ch5_seen 1
[b]@pistis--sofia posted[/b]



${nebu_1_2_notes} notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set nebu_1_2_notes + 1
        *set sofia_blue %+ 2
        *goto nebu_1_2
    *disable_reuse #Reblog üîÅ
        *set sofia_blue %+ 2
        *set sofia_interact + 1
        *set nebu_1_2_notes + 1
        *goto nebu_1_2
    #Prev post.
        *goto nebu_1_1
    #Look at another blog.
        *goto montage_1

*label trap_1_1
*set trap_prog 1
[b]${karla_bio}[/b]

[b]@trappedinspace posted[/b]

[essay in tags]

Tags: #okay so..., #i haven't been posting a lot lately for good reasons, #basically i've been super busy lately (in a good way!), #i'm in the md part of the md-phd program and it's going as well as it could have gone, #my life has been a cycle of epic highs and lows and i think this is one of the highs

${trap_1_1_notes} Notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set trap_1_1_notes + 1
        *set karla_blue %+ 5
        *goto trap_1_1
    *disable_reuse #Reblog üîÅ
        *set karla_interact + 1
        *set trap_1_1_notes + 1
        *goto trap_1_1
    #Next post.
        *goto trap_1_2
    #Look at another blog.
        *goto montage_1

*label trap_1_2
*set trap_prog 2
[b]@trappedinspace posted[/b]

Does anyone remember Sofia @nebulascribbler? Well, here are some things you should know.

She had been running a number of sockpuppet accounts pretending to be different people. One of which was @apocalypse-hoarder, where she pretended to be a minor to gain the trust of others and infiltrate a number of fandom spaces, and just stir up shit more generally. There is also evidence that @nebulascribbler, aka @apocalypse-hoarder, aka @greenrockmage, aka a dozen of other handles, had been responsible for a huge amount of the drama in the nebulaverse fandom over the past few years.

Here is the detailed evidence:

(Read more)

Tags: #thanks again to @${username} for a huge amount of this information!, #this post would not exist without ${em}, #sorry this took so long

${trap_1_2_notes} Notes

*choice
    *disable_reuse #Like ‚ù§Ô∏è
        *set trap_1_2_notes + 1
        *set karla_blue %+ 5
        *goto trap_1_2
    *disable_reuse #Reblog üîÅ
        *set karla_interact + 1
        *set trap_1_2_notes + 1
        *goto trap_1_2
    #Prev post.
        *goto trap_1_1
    #Look at another blog.
        *goto montage_1

*label luna_message_1

@icemoongirl: hey ${handle}

@icemoongirl: personal question: you're an incoming senior too, right? What are you doing for college apps?

*choice
    #I'm not going to college.
        @you: i'm probably not going to college.

        @icemoongirl: okay, that's cool!

        @icemoongirl: but i need some help with my college apps and i was wondering if you could still help?

        *goto luna_college_help
    #I'm going to a community college.
        @you: i'm probably going to community college.

        @icemoongirl: okay, cool!

        @icemoongirl: i'm applying to 4-year colleges and i was wondering if you could help...

        *goto luna_college_help
    #Yes, I'm applying mostly to local schools.
        @you: i'm applying to mostly local schools.

        @icemoongirl: okay, cool! me too, sort of.

        @icemoongirl: so i was wondering if you could help me out...

        *goto luna_college_help
    #Yes, I'm applying to the top schools.
        @you: i'm applying to all the top schools.

        @icemoongirl: cool! then you must be really good at school...

        @you: i don't know... maybe, i guess?

        @icemoongirl: i hope this isn't too much to ask, but i need some help...

        *label luna_college_help
        *set luna_message_ref "luna_college_help"

        @icemoongirl: i have to write an essay for a scholarship, and i've decided, what better topic to write about than the nebulaverse?

        @icemoongirl: cursed, i know.

        @icemoongirl: the topic is the same as one of the common app topics, asking about your background or interests.

        @icemoongirl: so, i've written an essay about how the nebulaverse fandom affected me, and how i ended up disconnecting from the fandom but not from the friends i made.

        @icemoongirl: i was wondering if you'd be willing to look through it?

        @icemoongirl: ofc you don't have to, but i'd be really glad if you read it and tell me what you think!

        *label luna_essay_will_read
        *choice
            *disable_reuse #Are you sure you should write about something so personal?
                @you: are you sure you should write about something so personal?

                @icemoongirl: haha i'm not mentioning that my headcanon essay was kind of the whole impetus for the whole thing, or anything about my place in the drama.

                @icemoongirl: i'm not even mentioning that i'm trans, not explicitly at least. a college essay isn't a coming out essay lol

                @icemoongirl: maybe i will write a detailed history of the gtm/nebulaverse affair one day. i have the notes already.
                *goto luna_essay_will_read
            *disable_reuse #Am I mentioned in it?
                @you: am i mentioned in that essay? :p

                @icemoongirl: um... not by name or anything like that

                @icemoongirl: you're... alluded to, i guess.
                *goto luna_essay_will_read
            *disable_reuse #Sure, I'll read it!
                *set luna_message_ref "luna_essay_read"
                *label luna_essay_read
                @you: sure, i'll read it!

                @icemoongirl: thank you so much!!! here's the link: [link to doc]

                @icemoongirl: tell me if it's terrible!

                *goto luna_other_qs
            *disable_reuse #I don't have time right now, get back to me on that.
                *set luna_message_ref "luna_essay_not_read"
                *label luna_essay_not_read
                @you: sorry, i don't have time right now. maybe later?

                @icemoongirl: that's okay! it's not due for a month. I'm just getting an early start for once lol

                *goto luna_other_qs

*label luna_other_qs
*set luna_message_ref "luna_other_qs"
*temp in_confession false
*choice
    #Bye, Luna...
        *label luna_goodbye
        *set luna_message_ref "luna_goodbye"

        @you: bye, luna...

        @icemoongirl: see you later, ${handle} üíôÔ∏è

        *goto montage_1
    *if (luna_blue > 50) #Um, Luna, how are you doing?
        *label luna_how_1
        *set luna_message_ref "luna_how_1"
        @you: um, luna, how are you doing, like, in general?

        @icemoongirl: um,... besides that huge diary entry i just posted? isn't that more information about me than you ever wanted to know?

        @icemoongirl: although i guess i'm doing, uh, worse than my blog posts suggest.

        *choice
            #Do you want to talk about that? (keep asking)
                @you: um, do you want to talk about that?
                *choice
                    #Yes, I want to hear...
                        *goto luna_secret
                    #Bye, Luna...
                        *goto luna_goodbye
            #Bye, Luna...
                *goto luna_goodbye
    *if (luna_confessed) #About that message I sent earlier (ask about ~romance~)...
        *label luna_rom_1
        *set luna_message_ref "luna_rom_1"
        *set in_confession true
        @you: um... about that one message I sent...

        @icemoongirl: oh. *that* message?

        @you: yeah...

        @icemoongirl: i was hoping i wouldn't have to talk about this

        @icemoongirl: honestly I hoped that you had forgotten...

        @icemoongirl: i mean, i thought about it a lot, but you might not want to hear what i thought...

        *choice
            #I still want to hear...
                @you: i still want to hear though...

                @icemoongirl: okay. can I tell you something?

                @you: of course, anything.

                @icemoongirl: i feel like no one knows what i'm really like, deep down. and if you did know what i was like deep down, you wouldn't have "feelings" for me or whatever.

                @you: try me. it's probably fine.

                @icemoongirl: ${handle}, i'm really, really fucked up.

                @icemoongirl: remember when i was running away and you talked me through it?

                @icemoongirl: that wasn't the first time i had an episode like that, and that might not be the last time either.

                @icemoongirl: uh this is gonna get dark i guess. so like... maybe we could talk about it later?

                *choice
                    #I still want to hear it.
                        @you: i still want to hear it, if that's okay? even if it's dark.
                        *label luna_secret
                        *set luna_blue %+ 5

                        @icemoongirl: alright, but, um, i warned you...

                        @icemoongirl: since the whole g.t.m thing happened, i haven't been able to move on.

                        @icemoongirl: sure i talk about moving on in my blog, but i still can't stop thinking about it. i say i'm doing better but i'm really not.

                        @icemoongirl: you'd think i'd have tougher skin by now, but if anything i'm weaker. what doesn't kill you doesn't make you stronger, it just messes you up for life.
                        
                        @icemoongirl: i read the comments on that one post "subtly" misgendering me and calling me a threat to women and stuff and i want to die.

                        @icemoongirl: but i can't stop reading them.

                        @icemoongirl: i think about hurting myself a lot. i think about dying. i think about what would happen to my corpse and what people would think. and i think about how it's all inevitable, so why not deal with it sooner. it's not like anything's getting better. everything's just gonna keep on getting worse until i die.

                        @icemoongirl: but then i have these moments when i'm just terrified. there's a part of me that wants to die and at the same time i'm so so afraid.

                        *if in_confession
                            @icemoongirl: and like... do you really want to deal with that? does anyone really want to deal with that?

                            @icemoongirl: so like... if you really *like* me, that's what you're getting. ignoring the fact that i'm physically ugly and like have a 0% chance of passing and all that. the real ugliness is inside.

                        *page_break ...

                        *if in_confession
                            @icemoongirl: oh wow i just realized how bad all that sounded. sorry about that.

                            @icemoongirl: i am not in any imminent danger right now so don't worry about me. i'm fine.

                            @icemoongirl: what were we talking about again?
                            
                            *label luna_confession_choices
                            *choice
                                *hide_reuse #*Hugs*
                                    @you: *hugs*

                                    @you: imagine i am hugging you right now.

                                    @icemoongirl: *you are hugging me right now*
                                    *goto luna_confession_choices
                                #I still like you (romantically, serious).
                                    @you: um, i still like you...

                                    @icemoongirl: no offense but that's kinda fucked up :/

                                    *label luna_romance_start

                                    @icemoongirl: so like... really? are you serious?

                                    @you: yeah.

                                    @icemoongirl: oh wow.

                                    @icemoongirl: you don't even know what i look like. i mean if you've seen my physical visage you'd know that there is nothing attractive about me...

                                    @you: i doubt that!

                                    @icemoongirl: so does that mean that you want to, um...
                                    *choice
                                        #Date?
                                            *achieve romance
                                            @you: date?

                                            @icemoongirl: yeah. that.

                                            *label luna_can_we
                                            *set luna_red + 10
                                            @you: yeah...

                                            @icemoongirl: oh god...

                                            @icemoongirl: like, this is the first time i've done any of this

                                            @icemoongirl: what are we even supposed to do?

                                            *fake_choice
                                                #I don't know either...
                                                    @you: um, i don't know either.

                                                    @icemoongirl: so we'll make it up as we go along, i guess?

                                                    *fake_choice
                                                        #Yeah, we'll make it all up as we go along!
                                                            @you: yeah, maybe we can make it up as we go along!

                                                            @icemoongirl: um, okay.

                                                            @icemoongirl: anyway, um... i love you. sorry. thanks. i don't know what to say.

                                                            @icemoongirl: üíôÔ∏Ñx100 i guess.

                                                            @you: ${heart_emoji}

                                                            *page_break A pause.
                                            *goto luna_chat_bye
                                        #Become romantically involved somehow?
                                            *achieve romance
                                            @you: become romantically involved somehow?

                                            @icemoongirl: yeah, that...

                                            *goto luna_can_we
                                        #Get... together?
                                            *achieve romance
                                            @you: um, get... together?

                                            @icemoongirl: yeah, that.

                                            *goto luna_can_we
                                #We can forget about this conversation.
                                    *goto luna_chat_bye
                                *hide_reuse #Do you have anyone else you can talk to? I'm worried about you.
                                    @you: do you have anyone else you can talk to? i'm worried about you.

                                    @icemoongirl: nope! if i ever talked to any adults like this i'd be institutionalized.

                                    @icemoongirl: i'm pretty sure the only therapy i'd ever get is conversion therapy lmao
                                    *goto luna_chat_bye
                        *else
                            *achieve friends
                            @icemoongirl: oh wow i just realized how bad all that sounds.

                            @icemoongirl: i am not in any imminent danger right now so don't worry about me.

                            @icemoongirl: i'm really sorry about the rant lol.

                            @you: it's okay...
                            *label luna_friend_choices
                            *choice
                                *hide_reuse #*Hugs*
                                    @you: *hugs*

                                    @you: imagine i am hugging you right now.

                                    @icemoongirl: *you are hugging me right now*

                                    @icemoongirl: thanks.

                                    *goto luna_chat_bye
                                #Thanks for telling me this.
                                    @you: thank you for telling me all this.

                                    @icemoongirl: thank you for listening...

                                    *goto luna_chat_bye
                                #I've felt the same way sometimes.
                                    @you: um, thank you for telling me this. i've felt the same way sometimes.

                                    @icemoongirl: oh, really? that sucks.

                                    @you: yeah...

                                    @icemoongirl: sorry, i don't know what to say to that.

                                    *page_break A pause.

                                    *goto luna_chat_bye
                                #Do you have anyone else you can talk to? I'm worried about you.
                                    @you: do you have anyone else you can talk to? i'm worried about you.

                                    @icemoongirl: nope! if i ever talked to any adults like this i'd be institutionalized.

                                    @icemoongirl: or worse.

                                    @icemoongirl: but it's fine. i'm fine. i'll live. don't worry.

                                    *goto luna_chat_bye
                                *if (luna_confessed) #I like you (romantically)...
                                    @you: i like you.

                                    @icemoongirl: what does that mean?

                                    @you: yeah

                                    @icemoongirl: wait, is this a romantic confession? after i said all *that*?

                                    @icemoongirl: are you out of your mind?

                                    *choice
                                        #I'm not out of my mind.
                                            @you: i'm not out of my mind.

                                            *goto luna_romance_start
                                        #I might be a little out of my mind.
                                            @you: i might be a little out of my mind.

                                            *goto luna_romance_start

                    #Let's talk about it later.
                        @you: maybe we can talk about this later.

                        @icemoongirl: sure. that'd be fine.

                        *goto luna_chat_bye
                    #We don't have to talk about it.
                        @you: maybe we don't have to talk about it.

                        @icemoongirl: yeah. maybe we don't.
                
                        *goto luna_chat_bye
            #Maybe it's better to forget...
                @you: maybe it's better to forget anything i said.

                @icemoongirl: maybe that's better.
                *goto luna_chat_bye

*label luna_chat_bye
*set luna_message_ref "luna_chat_bye"

@icemoongirl: anyway, bye ${handle} üíôÔ∏è

*if luna_red > 2
    @icemoongirl: i love you.

    @icemoongirl: wait is that too soon to say. does it cheapen the gesture.

    @you: um, i think it's fine...

    @icemoongirl: üíôÔ∏è

@icemoongirl: see you soon.

@you: bye ${heart_emoji}

*goto montage_1



*label sofia_message_1

@pistis--sofia: Hey $!{handle}.

@pistis--sofia: This is Sofia - formerly @nebulascribbler, if it isn't obvious enough already.

@pistis--sofia: Do you still remember me?

*choice
    #Of course I remember you.
        @you: of course

        @pistis--sofia: Good.
    #No, who the heck are you?
        @you: no, who the heck are you?

        @pistis--sofia: Haha.

@pistis--sofia: I wanted to talk to you.

@pistis--sofia: How are you doing, by the way? Are you doing alright, now that the nebulaverse fandom has collapsed?
*comment TODO

*goto montage_1


*label annc_1

[b]Anti-Nebulaverse Nebulaverse Club[/b]

@trappedinspace: Hey Luna?

@icemoongirl: yeah?

@trappedinspace: I'm writing a history of the nebulaverse fandom and the whole deal with GTM, and, well, you can probably guess where this is going.

@trappedinspace: This feels like a historical moment that people will still care about in a few years' time. It would be really nice if you could contribute to this project.

@icemoongirl: rly? that sounds... uh... idk if i really want that

@trappedinspace: Sorry, you definitely don't have to do this! I got kind of carried away.

@icemoongirl: um, it's not that i don't want to do it. more that i'm not sure if i'm ready to be *out there*. i don't want to become a personality anymore. right now i just want some space to exist.

@icemoongirl: maybe i'll tell that story one day. i just hope i'll live to that day lol

@trappedinspace: I hope we will, too.

@icemoongirl: sorry that was dark. um, i'd be very interested in reading what you've written!

@trappedinspace: Yeah, so, right now it's just some very scattered notes. There's nothing coherent or organized yet, no narrative. But I'll get a link soon.

@icemoongirl: thanks üíôÔ∏è

*if karla_end
    @trappedinspace: BTW @${username}!

    @trappedinspace: Thanks again for the help with @nebulascribbler/@apocalypse-hoarder. It would be very cool if you could be a part of this too. What do you think?

    *choice
        #Yes, I'd love to be part of fandom history!
            *label annc_k_yes
            *set annc_ref "annc_k_yes"
            @you: sure, i'd love to be a part of fandom history!

            @trappedinspace: Great!

            *goto annc_k_anyway
        #No, I'd rather not.
            *label annc_k_no
            *set annc_ref "annc_k_no"
            @you: no, i'd rather not...

            @trappedinspace: That's okay...

            *goto annc_k_anyway
        #I don't know yet.
            *label annc_k_maybe
            *set annc_ref "annc_k_maybe"
            @you: um, i don't know yet...

            @trappedinspace: That's okay!

            *label annc_k_anyway

            @trappedinspace: Anyway...

            @trappedinspace: This is just a hypothetical project for now. Maybe it'll never see the light of day.

            @trappedinspace: And honestly I do not have the free time for more fandom side projects.
            *choice
                #Go back.
                    *goto montage_1
*else
    *choice
        #Go back.
            *goto montage_1

*label claire_message_1

*comment Claire offers you a beta read for their book, and also asks for forgiveness from Luna.

@shadow-protectrix: Hey $!{handle}!

@shadow-protectrix: I was just wondering,  

TODO

*choice
    #Go back.
        *goto montage_1

*label karla_message_1

TODO

*choice
    #Go back.
        *goto montage_1



*label montage1_end

The story of the Shadow Nebula is over. What will you do now?

*page_break End.
*ending
